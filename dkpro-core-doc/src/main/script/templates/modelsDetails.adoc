<%
import de.tudarmstadt.ukp.dkpro.core.doc.Util;

def metadataValueText(model, key, value)
{
    if (key.endsWith(".tagset")) {
        def layer = key.tokenize('.')[0];
        def tagset = value;
        def lang = model.@language;
        return "<<tagset-reference.adoc#tagset-${lang}-${tagset}-${layer},${ tagset }>>";
    }
    else {
        return value;
    }
}

def getEngineLink(model) {
    if (model.@engine as String) {
        return Util.engineLink(model.@engine);
    } else {
        return "`${model.@tool}` @ `${model.@package}`";
    } 
}

def getSource(model) {
    def source;
    def getStatement = model.parent().children().find { node -> node.name() in [ 'get' ]};
    if (getStatement?.@src) {
        source = getStatement?.@src.text()
    }
    else {
        source = "No get statement found";
    }
    return source;
}

def renderModel(model) {
%>
[[model-${model.@artifactId}]]
==== ${model.@variant.text() } 

****
[horizontal, role="small"]
Group{nbsp}ID::    ${model.@groupId}
Artifact{nbsp}ID:: ${model.@artifactId}
Version::          ${model.@version}
Engine::           ${getEngineLink(model)}
Language::         ${model.@language}
Variant::          ${model.@variant} <% 
if (getSource(model)) { %>
Source::           ${getSource(model)} <% 
}
if (model.@md5.text()) { %>
MD5::              ${model.@md5.text()}
<% } %>
****


<% if (model.metadata.entry.size() > 0) { %>
.Metadata for ${model.@shortArtifactId} 
[options="header", role="small"]
|====
|Key|Value
<% model.metadata.entry.each { %>
| ${ it.@key }
| ${ metadataValueText(model, it.@key as String, it.@value as String) }
<% } %>
|====
<% } // if (model.metadata.entry) %>
<% 
}
%>

<% 
models
    .findAll { it.@engine as String }
    .groupBy { it.@language.text() }
    .sort { it.key }
    .each { language, languageGroup ->
%>
== ${language} (${languageGroup.size()})
<%
    languageGroup.groupBy { it.@engine.text() }.sort { it.key }.each { engine, engineGroup ->
%>
=== ${engine} (${engineGroup.size()})

<%
        engineGroup.sort { it.@variant.text() }.each { renderModel(it) }
    }
}
%>
